plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id "io.freefair.aspectj" version '6.6.3'
    id 'org.jreleaser' version '1.16.0'
}

group 'io.github.thanglequoc'
version '1.0.3-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = "11"
    targetCompatibility = "11"
}

repositories {
    mavenCentral()
}

dependencies {

    implementation group: 'org.aspectj', name: 'aspectjrt', version: '[1.9.19,)'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '[2.0.7,)'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'

    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.6'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.6'
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'timer-ninja'
            from components.java

            pom {
                name = 'Timer Ninja'
                description = 'A handy Java utility to help you evaluate the total execution time of your Java method'
                url = 'https://github.com/ThangLeQuoc/timer-ninja'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'thanglequoc'
                        name = 'Thang Le Quoc'
                        email = 'thanglequoc.it@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ThangLeQuoc/timer-ninja.git'
                    developerConnection = 'scm:git:ssh://github.com/ThangLeQuoc/timer-ninja.git'
                    url = 'https://github.com/ThangLeQuoc/timer-ninja'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
        mavenLocal()
    }
}

jreleaser {
    project {
        name = 'timer-ninja'
        authors = ['ThangLeQuoc']
        maintainers = ['ThangLeQuoc']
        license = 'Apache-2.0'
        links {
            homepage = 'https://github.com/ThangLeQuoc/timer-ninja'
        }
        description = 'A handy Java utility to help you measure execution time of Java code'
        longDescription = 'Timer Ninja is a brainless Java library to evaluate your Java code method execution time. Timer Ninja follows the Aspect Oriented Programming (AOP) concept, backed by AspectJ, to make it super simple to declare tracker on method.'
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = 'changeit'
                    password = 'changeit'
                    authorization = 'BEARER'
                    sign = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }

    signing {
        active = 'RELEASE'
        armored = true
        mode = 'FILE'
        files = true
        artifacts = true
        checksums = true

        passphrase = 'changeit'
        publicKey = './public_key.asc'
        secretKey = './secret_key.asc'

    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
