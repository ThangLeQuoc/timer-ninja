plugins {
    id 'java-library'
    id "io.freefair.aspectj" version '6.6.3'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.thanglequoc'
version '1.0.0'
sourceCompatibility = '11'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {

    implementation group: 'org.aspectj', name: 'aspectjrt', version: '[1.9.19,)'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '[2.0.7,)'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.6'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.6'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'timer-ninja'
            from components.java

            pom {
                name = 'Timer Ninja'
                description = 'A handy Java utility to help you evaluate the total execution time of your Java method'
                url = 'https://github.com/ThangLeQuoc/timer-ninja'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'thanglequoc'
                        name = 'Thang Le Quoc'
                        email = 'thanglequoc.it@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ThangLeQuoc/timer-ninja.git'
                    developerConnection = 'scm:git:ssh://github.com/ThangLeQuoc/timer-ninja.git'
                    url = 'https://github.com/ThangLeQuoc/timer-ninja'
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'sonartype'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.property('ossrhUsername')
                password = project.property('ossrhPassword')
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

//task javadocJar(type: Jar) {
//    archiveClassifier = 'javadoc'
//    from javadoc
//}
//
//task sourcesJar(type: Jar) {
//    archiveClassifier = 'sources'
//    from sourceSets.main.allSource
//}

